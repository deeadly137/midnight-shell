pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Caelestia.Config
import qs.components
import qs.services

Item {
    id: root

    required property Repeater workspaces
    required property var occupied
    required property int groupOffset
    required property bool layoutTransitionRunning
    required property var workspaceIndex

    // Horizontal (top/bottom bar) support
    readonly property bool isHorizontal: Config.bar.position === "top" || Config.bar.position === "bottom"

    property list<var> pills: []

    onOccupiedChanged: {
        if (!occupied)
            return;
        let count = 0;
        const start = groupOffset;
        const end = start + Config.bar.workspaces.shown;
        for (const [ws, occ] of Object.entries(occupied)) {
            if (ws > start && ws <= end && occ) {
                const isFirstInGroup = Number(ws) === start + 1;
                const isLastInGroup = Number(ws) === end;
                if (isFirstInGroup || !occupied[ws - 1]) {
                    if (pills[count])
                        pills[count].start = ws;
                    else
                        pills.push(pillComp.createObject(root, {
                            start: ws
                        }));
                    count++;
                }
                if ((isLastInGroup || !occupied[ws + 1]) && pills[count - 1])
                    pills[count - 1].end = ws;
            }
        }
        if (pills.length > count)
            pills.splice(count, pills.length - count).forEach(p => p.destroy());
    }

    Repeater {
        model: ScriptModel {
            values: root.pills.filter(p => p)
        }

        StyledRect {
            id: rect

            required property var modelData

            readonly property Workspace start: {
                root.workspaces.count;
                return root.workspaces.itemAt(root.workspaceIndex(modelData.start)) as Workspace ?? null;
            }
            readonly property Workspace end: {
                root.workspaces.count;
                return root.workspaces.itemAt(root.workspaceIndex(modelData.end)) as Workspace ?? null;
            }

            anchors.horizontalCenter: isHorizontal ? undefined : root.horizontalCenter
            anchors.verticalCenter: isHorizontal ? root.verticalCenter : undefined

            x: isHorizontal ? ((start?.x ?? 0) - 1) : 0
            y: isHorizontal ? 0 : ((start?.y ?? 0) - 1)
            implicitWidth: isHorizontal ? (start && end ? end.x + end.size - start.x + 2 : 0) : (Tokens.sizes.bar.innerWidth - Tokens.padding.small + 2)
            implicitHeight: isHorizontal ? (Tokens.sizes.bar.innerWidth - Tokens.padding.small + 2) : (start && end ? end.y + end.size - start.y + 2 : 0)

            color: Colours.layer(Colours.palette.m3surfaceContainerHigh, 2)
            radius: Tokens.rounding.full

            scale: 0
            Component.onCompleted: scale = 1

            Behavior on scale {
                Anim {
                    easing: Tokens.anim.standardDecel
                }
            }

            Behavior on x {
                enabled: isHorizontal && !root.layoutTransitionRunning

                Anim {}
            }

            Behavior on y {
                enabled: !isHorizontal && !root.layoutTransitionRunning

                Anim {}
            }

            Behavior on implicitWidth {
                enabled: isHorizontal && !root.layoutTransitionRunning

                Anim {}
            }

            Behavior on implicitHeight {
                enabled: !isHorizontal && !root.layoutTransitionRunning

                Anim {}
            }
        }
    }

    Component {
        id: pillComp

        Pill {}
    }

    component Pill: QtObject {
        property int start
        property int end
    }
}
